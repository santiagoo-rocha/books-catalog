plugins {
    id 'java'
    id 'jacoco'
}

group = 'com.moneygram'
version = '0.0.1-SNAPSHOT'
description = 'demo'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(23)
    }
}

repositories {
    mavenCentral()
}

tasks.register('jacocoRootReport', JacocoReport){
    dependsOn = subprojects.tasks.test
    additionalSourceDirs.from = subprojects.sourceSets.main.allSource.srcDirs
    sourceDirectories.from = subprojects.sourceSets.main.allSource.srcDirs
    classDirectories.from = subprojects.sourceSets.main.output
    executionData.from = subprojects.jacocoTestReport.executionData

    reports {
        html.required = true
        xml.required = true
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'jacoco'

    repositories {
        mavenCentral()
    }

    testing { suites { test { useJUnitJupiter() } } }

    tasks.withType(Test).configureEach {
        finalizedBy(tasks.jacocoTestReport)
    }

    tasks.jacocoTestReport {
        dependsOn(tasks.test)
        reports {
            xml.required.set(true)
            html.required.set(true)
        }
    }

    dependencies {
        testImplementation platform('org.junit:junit-bom:5.12.2')
        testImplementation 'org.junit.jupiter:junit-jupiter-api'
        testImplementation 'org.junit.jupiter:junit-jupiter-params'
        testRuntimeOnly  'org.junit.jupiter:junit-jupiter-engine'
        testImplementation 'org.mockito:mockito-core:5.19.0'
        testImplementation 'org.mockito:mockito-junit-jupiter:5.19.0'
    }
}


